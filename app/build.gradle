apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

configurations {
    envProdDebugCompile
    envProdReleaseCompile
    envDevDebugCompile
    envDevReleaseCompile
}

ext {
    // ref: https://www.jayway.com/2015/03/11/automatic-versioncode-generation-in-android-gradle/
    buildVersionCode = { version ->
        def (major, minor, patch) = version.tokenize('.')
        (major, minor, patch) = [major, minor, patch].collect { it.toInteger() }
        major * 1000000 + minor * 1000 + patch
    }
}

android {
    // セマンティックバージョニングを利用してください
    version "1.0.0"
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "com.tumpaca.tumpaca"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode buildVersionCode(version)
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        envDev {
            buildConfigField "boolean", "FIREBASE_ENABLED", "false"
        }
        envProd {
            buildConfigField "boolean", "FIREBASE_ENABLED", "true"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    dexOptions {
        dexInProcess true
        javaMaxHeapSize "4g"
        preDexLibraries true
    }
}

preBuild {
    // TumblrのAPIキーをlocal.propertiesから読み込んでアプリに組み込まれるリソースを生成
    def authFile = file('src/main/res/raw/auth.properties')
    outputs.file authFile
    doLast {
        def localProps = new Properties()
        rootProject.file('local.properties').withReader { localProps.load it }
        def props = new Properties()
        ['tumblr.consumer.key', 'tumblr.consumer.secret'].each {
            def val = localProps.get(it)
            if (val == null) {
                throw new GradleException("$it は定義されていない。 local.properties にて定義してください。")
            }
            props.putAll((it): val.toString().bytes.encodeBase64().toString())
        }
        authFile.parentFile.mkdirs()
        authFile.withWriter { props.store it, null }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.1.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.daksh:loglr:1.0.0'
    compile 'com.google.firebase:firebase-analytics:9.0.0'
    // firebase-crash はコード上で on/off を切り替えられないので依存性の有無によって切り替える
    envProdReleaseCompile 'com.google.firebase:firebase-crash:9.0.0'
    envProdDebugCompile 'com.google.firebase:firebase-crash:9.0.0'
    compile project(':jumblr')
    compile project(':GifImageView:library')
}
repositories {
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
